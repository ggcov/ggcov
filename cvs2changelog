#!/bin/sh
#
# ggcov - A GTK frontend for exploring gcov coverage data
# Copyright (c) 2001-2004 Greg Banks <gnb@users.sourceforge.net>
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
# 
# $Id: cvs2changelog,v 1.11 2010-05-09 05:37:14 gnb Exp $
#
# Shell script to build a change log for a new release
# from CVS logs. The change log will still need some
# manual editing to re-express changes for users, etc.
#

REV1=
REV2=
PACKAGE=`sed -n -e 's|^AM_INIT_AUTOMAKE(\([^,]\+\),\([^)]\+\))|\1|p' < configure.in`
DRYRUN=no
NREVS=0
ME="Greg Banks"
EMAIL="gnb@users.sourceforge.net"
#FILES="Makefile.in Makefile configure.in"
DATE_FORMAT="%a %b %_d %H:%M:%S %Y"
CHECKFILE="configure.in"
IGNOREDIRS="www doc po"
LOG=ChangeLog
INFILE=
OLDLOG=ChangeLog.$$
RAWMODE=no
VERBOSE=0

canon_tag ()
{
    # Usage: canon_tag rev
    echo "${PACKAGE}_$1" | tr a-z. A-Z_
}

get_last_rev_tag ()
{
    TAGPREFIX=`canon_tag ""`
    cvs log -h $CHECKFILE |\
    	awk '/^	'$TAGPREFIX'[0-9_]*:/{print substr($1,1,length($1)-1); exit}'
	
}

get_rev_date ()
{
    # Usage: get_rev_data rev
    local tag
    local cvsdate
    
    tag=`canon_tag $1`
    cvsdate=`cvs log $CHECKFILE |\
    	awk '
/^symbolic names:/ {
    state = 1;
}
/^----------------------------$/ {
    state = 2;
}
/^[ 	]*'$tag':/ {
    if (state == 1)
    	frev = $2;
}
/^revision / {
    if ($2 == frev)
    	state = 4;
}
/^date:/ {
    if (state == 4)
    {
    	printf "%s %s GMT\n", gensub("/", "-", "g", $2), gensub(";", "", "g", $3);
    	exit;
    }
}
'`
    date +"$DATE_FORMAT" -d "$cvsdate"
}

usage ()
{
    [ -z "$*" ] || echo "cvs2changelog: $*"
    echo "Usage: cvs2changelog [--package pkg] [--dryrun] [rev1 [rev2]]"
    exit 0
}

while [ $# -gt 0 ]; do
    case "$1" in
    --package=*) PACKAGE=`echo "$1"|sed -e 's|^[^=]*=||g'` ;;
    --package) PACKAGE="$2" ; shift ;;
    --dryrun) DRYRUN=yes ;;
    --raw) RAWMODE=yes ;;
    --infile=*) INFILE=`echo "$1"|sed -e 's|^[^=]*=||g'` ;;
    --infile) INFILE="$2" ; shift ;;
    --help) usage ;;
    --verbose) VERBOSE=1 ;;
    -*) usage "bad option \"$1\"" ;;
    [0-9]*.[0-9]*.[0-9]*|[0-9]*.[0-9]*)
    	case "$NREVS" in
	0) REV1="$1" ;;
	1) REV2="$1" ;;
	*) usage "too many revisions at \"$1\"" ;;
	esac
	NREVS=`expr $NREVS + 1`
	;;
    *) usage "unexpected argument at \"$1\"" ;;
    esac
    shift
done

if [ -n "$INFILE" -a ! -f "$INFILE" ]; then
    echo "$INFILE: No such file"
    exit 1
fi

test -z "$PACKAGE" && usage "no package specified"

if [ -z "$REV1" ]; then
    # calculate previous tagged revision by getting
    # most recent tag of the correct format
    TAG1=`get_last_rev_tag`
else
    TAG1=`canon_tag "$REV1"`
fi

if [ -z "$REV2" ]; then
    # Use the magic tag `HEAD' which indicates the
    # most recent revision on a per-file basis
    TAG2="HEAD"
    REV2="latest revision"
    DATE=`date +"$DATE_FORMAT"`
    RELEASE=
else    
    TAG2=`canon_tag "$REV2"`
    if [ -n "$INFILE" ]; then
	DATE=`date +"$DATE_FORMAT"`
    else
	DATE=`get_rev_date $REV2`
    fi
    RELEASE="$REV2"
fi

# echo "PACKAGE=$PACKAGE"
# echo "REV1=$REV1"
# echo "TAG1=$TAG1"
# echo "REV2=$REV2"
# echo "TAG2=$TAG2"
# echo "DRYRUN=$DRYRUN"
# exit 1

(
    echo -n "Compiling changes from $PACKAGE tags $TAG1 to $TAG2"
    test "$DRYRUN" = no && echo -n " into $LOG"
    echo ""
) 1>&2

# Extract change messages from multiple-file revision
# history.  If the log contains a symbolic name matching
# $TAG1, we drop the corresponding message because "cvs log"
# emits one revision too many in that case.  Otherwise,
# the file was created since $TAG1 and we use all messages.
#
# state:
# 0 = reading log header
# 1 = reading log lines
# 2 = reading symbolic names
extract ()
{
    awk -v TAG1=$TAG1 -v TAG2=$TAG2 -v verbose=$VERBOSE '
BEGIN {
    state = 0;
    inattic = 0;
    nlines = 0;
    lines[0] = "";
    tag1rev = "";
    tag2rev = "";
    rev = "";
}
function same_branch(r1, r2,     a1, a2, n1, n2, i) {
    n1 = split(r1, a1, ".");
    n2 = split(r2, a2, ".");
    if (n1 != n2) return 0;
    for (i = 1 ; i <= (n1-2) ; i++) {
    	if (a1[i] != a2[i]) return 0;
    }
    return 1;
}
function compare(r1, r2,     a1, a2, n1, n2, nm, i) {
    n1 = split(r1, a1, ".");
    n2 = split(r2, a2, ".");
    nm = (n1 < n2 ? n1 : n2);
    for (i = 1 ; i <= nm ; i++) {
	if (a1[i] > a2[i])
	    return 1;
	else if (a1[i] < a2[i])
	    return -1;
    }
    return 0;
}
/^head:/ {
    if (TAG1 == "HEAD") {
    	tag1rev = $2;
#    	printf "172: tag1rev=\"%s\"\n", tag1rev;
    }
    if (TAG2 == "HEAD") {
    	tag2rev = $2;
#    	printf "176: tag2rev=\"%s\"\n", tag2rev;
    }
}
/^symbolic names:/ {
    state = 2;
    next;
}
/^keyword substitution:/ {
    state = 0;
}
/^date: / {
    if (state == 0) {
    	state = 1;
	nlines = 0;
	next;
    }
}
/^Working file:/ {
    if (verbose)
	printf "******* %s\n", $0;
    fname=$3;
}
/^RCS file: .*\/Attic\// {
    if (state == 0) {
    	inattic = 1;
	next;
    }
}
/^revision [0-9.]*$/ {
    if (state == 0) {
    	rev = $2;
	if (verbose)
	    printf "183: rev=%s\n", rev;
    }
}
/^----------------/ {
    for (i = 0 ; i < nlines ; i++)
    	print lines[i];
    print "";
    state = 0;
}
/^================/ {
    for (i = 0 ; i < nlines ; i++)
    	print lines[i];
    print "";
    state = 0;
    inattic = 0;
    nlines = 0;
    tag1rev = "";
    tag2rev = "";
}
{
#    printf "197: fname=\"%s\" state=%d inattic=%d rev=\"%s\" tag1rev=\"%s\" tag2rev=\"%s\"\n", fname, state, inattic, rev, tag1rev, tag2rev >"/dev/stderr";
    if (state == 1 && !inattic && compare(rev, tag1rev) > 0 && same_branch(rev, tag2rev)) {
	if (verbose)
	    printf "229: using %s:%s\n", fname, rev # >"/dev/stderr";
	lines[nlines++] = $0;
    } else if (state == 2 && $1 == TAG1":") {
    	tag1rev = $2;
	if (verbose)
	    printf "205: tag1rev=\"%s\"\n", tag1rev;
    } else if (state == 2 && $1 == TAG2":") {
    	tag2rev = $2;
	if (verbose)
	    printf "236: tag2rev=\"%s\"\n", tag2rev;
    }
}'
}

# Reformat the various strange ways I do checkin messages
# into something approaching GNU ChangeLog format.
canonicalise ()
{
    awk '
BEGIN {
    line = "";
    prefix = "\t* ";
    join = "\n\t";
#    prefix = "X";
#    join = "Y";
    mode = 0;
}
{
    if ($0 == "") {
    	if (line != "") print line;
    	line = "";
    } else if (match($0, "^[*x](  +|\t+)")) {
    	if (line != "") print line;
	mode = 1;
	line = prefix""substr($0, RLENGTH+1, length($0)-RLENGTH);
    } else if (match($0, "^(   +|\t+)")) {
    	if (mode != 1 && line != "") {
	    print line;
	    line = "";
	}
	mode = 1;
	line = line""join""substr($0, RLENGTH+1, length($0)-RLENGTH);
    } else {
    	if (mode != 2 && line != "") {
	    print line;
	    line = "";
	}
	mode = 2;
	if (line == "")
	    line = prefix""$0;
	else
	    line = line""join""$0;
    }
}
END {
    if (line != "") print line;
}'
}

# Remove duplicate log entries, usually due to the same
# log message (or point in a log message) being used for
# multiple files.
uniquify ()
{
    awk '
BEGIN {
    alldesc[""] = 0;
    desc = "";
}
/^$/ {
    next;
}
/^\t\* / {
    alldesc[desc]++;
    desc = $0;
    next;
}
/^\t/ {
    desc = desc"\n"$0
}
END {
    alldesc[desc]++;
    for (i in alldesc) {
    	if (i != "") print i;
    }
}'
}

# Rawmode isn't quite raw; we remove empty lines and duplicate messages.
rawuniquify ()
{
    sed -e 's/^[ \t]*$//' | awk '
BEGIN {
    line = "";
    mode = 0;
}
function emit_line () {
    if (++ulines[line] == 1) {
    	print line;
    }
}
{
#    print ">>>\""$0"\"";
    if ($0 == "") {
    	if (line != "") emit_line();
    	line = "";
	mode = 0;
    } else if (match($0, "^[*x](  +|\t+)")) {
    	if (line != "") emit_line();
	mode = 1;
	line = $0;
    } else if (match($0, "^(   +|\t+)")) {
    	if (mode != 1 && line != "") {
	    emit_line();
	    line = "";
	}
	mode = 1;
	line = line"\n"$0;
    } else {
    	if (mode != 2 && line != "") {
	    emit_line();
	    line = "";
	}
	mode = 2;
	if (line == "")
	    line = $0;
	else
	    line = line"\n"$0;
    }
}
END {
    if (line != "") emit_line();
}'
}

getDirs ()
{
    IGNOREEXP=`echo "$IGNOREDIRS" | sed -e 's:[ 	][  ]*:|:g'`
    find . -type d -name CVS -print |\
    	sed -e '/^\.\/CVS$/d' -e 's/\/CVS$//' -e 's/^\.\///' |\
	egrep -v '^('$IGNOREEXP')$'
}

cookedGatherChanges ()
{
    echo "$DATE  $ME <$EMAIL>"
    test -z "$RELEASE" || echo "	* Release $RELEASE"
    if [ -z "$FILES" ]; then
    	FILES="-l . "`getDirs`
    fi
    if [ -n "$INFILE" ]; then
	cat $INFILE | extract | canonicalise | uniquify
    else
	cvs log -r${TAG1}:${TAG2} $FILES 2>&1 | extract | canonicalise | uniquify
    fi
    echo ""
}

rawGatherChanges ()
{
    if [ -z "$FILES" ]; then
    	FILES="-l . "`getDirs`
    fi
    if [ -n "$INFILE" ]; then
	cat $INFILE | extract | rawuniquify
    else
	cvs log -r${TAG1}:${TAG2} $FILES 2>&1 | extract | rawuniquify
    fi
}

gatherChanges ()
{
    if [ $RAWMODE = yes ]; then
    	rawGatherChanges
    else
    	cookedGatherChanges
    fi
}

if [ "$DRYRUN" = yes ] ; then
    gatherChanges
else
    mv $LOG $OLDLOG && \
    gatherChanges > $LOG && \
    cat $OLDLOG >> $LOG && \
    rm $OLDLOG
fi
